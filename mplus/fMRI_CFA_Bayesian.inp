TITLE: fMRI Ordinal/Bayesian CFA & Comp of Loadings

DATA:   FILE IS "C:fMRI_qual_mplus.csv" ;  

VARIABLE:
      NAMES = rt1 rt2 rt3 rt7 rt8 rt9 rt13
                rt14 rt15 rt19 rt20 rt21 rt25 rt26 rt27;
      CATEG = rt1 rt2 rt3 rt7 rt8 rt9 rt13
                      rt14 rt15 rt19 rt20 rt21 rt25 rt26 rt27;
       missing = all(999);

ANALYSIS:
    ESTIMATOR = BAYES; !Must specify BAYES here to run Bayesian estimation.
    BITERATION=70000; !Indicates the number of MCMC iterations to be used.
    POINT=MEDIAN; !Statistic used to as point estimate for the posterior distributions.
    PROCESSORS=2; !Specifies the number of MCMC chains to run.
    ALGORITHM = GIBBS(PX1); !Specifies the type of MCMC algorithm to be used.
    BSEED = 3; !Random seed to start the MCMC chains.

MODEL:
    F1 BY
    rt1* rt2 rt3 rt7 rt9 rt13 rt19 rt25 rt26 rt27(f1 f2 f3 f4 f5 f6 f7 f8 f9 f10);
    F2 BY
    rt8* rt14 rt15 rt20 rt21(f11 f12 f13 f14 f15);

    F1@1; !Fix latent variable variance to 1
   [F1@0]; ! Fix mean to zero

    F2@1; !Fix latent variable variance to 1
   [F2@0]; ! Fix mean to zero

   F1 WITH F2 (cov1);

MODEL PRIORS:
    f1-f15~N(0, 1);
    cov1 ~IW(1, 3);

model constraint:
    new(dif1-dif15);
    dif1 = f1 - .765;
    dif2 = f2  - .785;
    dif3 = f3  - .939;
    dif4 = f4  - 1.184;
    dif5 = f5  - .932;
    dif6 = f6  - 1.560;
    dif7 = f7  - .986;
    dif8 = f8  - .744;
    dif9 = f9  - .529;
    dif10 = f10  - 1.828;
    dif11 = f11  - 1.965;
    dif12 = f12  - .693;
    dif13 = f13  - .995;
    dif14 = f14  - 1.931;
    dif15 = f15  - 1.173;

OUTPUT:  !STAND;
    TECH8; !Will display the potential scale reduction factor.

PLOT:
    TYPE=PLOT3; !Supplies the posterior histograms, trace and autocorrelation plots.
